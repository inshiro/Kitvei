apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'
configurations {
    all*.exclude group: 'com.android.support'
}

// Versioning Naming Scheme: is.gd/j8L2Wm AND is.gd/IfAfAX

ext.versionRelease = 2
ext.versionBreaking = 3
ext.versionFeature = 0
ext.versionFix = 2

android {
    compileSdkVersion 28
    buildToolsVersion "28.0.3"
    defaultConfig {
        applicationId "na.kephas.kitvei"
        targetSdkVersion 28
        minSdkVersion 14
        versionCode generateVersionCode() // 10020103
        versionName generateVersionName() // 1.2.3
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        resConfigs "en"

    }
    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }
    packagingOptions {
        exclude 'META-INF/DEPENDENCIES'
        exclude 'META-INF/LICENSE'
        exclude 'META-INF/LICENSE.txt'
        exclude 'META-INF/license.txt'
        exclude 'META-INF/NOTICE'
        exclude 'META-INF/NOTICE.txt'
        exclude 'META-INF/notice.txt'
        exclude 'META-INF/ASL2.0'
        exclude 'META-INF/main.kotlin_module'
    }
    lintOptions {
        abortOnError false
        checkReleaseBuilds false
    }
    testOptions {
        unitTests {
            includeAndroidResources = true
        }
    }
}

private Integer generateVersionCode() {
    return ext.versionRelease * 10000000 + ext.versionBreaking * 10000  + ext.versionFeature * 100 + ext.versionFix
}

private String generateVersionName() {
    return "${ext.versionRelease}.${ext.versionBreaking}.${ext.versionFeature}.${ext.versionFix}"
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    // Android support library
    def androidx_verison = "1.0.0"
    implementation "com.google.android.material:material:$androidx_verison"
    implementation "androidx.appcompat:appcompat:$androidx_verison"
    implementation "androidx.recyclerview:recyclerview:$androidx_verison"
    implementation "androidx.core:core-ktx:$androidx_verison"
    implementation "androidx.constraintlayout:constraintlayout:2.0.0-alpha3"

    // ViewModel-LiveData
    def lifecycle_version  = "2.0.0"
    implementation "androidx.lifecycle:lifecycle-extensions:$lifecycle_version"
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:$lifecycle_version"
    kapt "androidx.lifecycle:lifecycle-compiler:$lifecycle_version"

    // Navigation
    /*def nav_version = "1.0.0-alpha01"
    implementation "android.arch.navigation:navigation-fragment-ktx:$nav_version"
    implementation "android.arch.navigation:navigation-ui-ktx:$nav_version"
    androidTestImplementation "android.arch.navigation:navigation-testing-ktx:$nav_version"*/

    // Room
    def room_version  = "2.0.0"
    implementation "androidx.room:room-runtime:$room_version"
    kapt "androidx.room:room-compiler:$room_version"

    // Paging
    def paging_version = "2.0.0"
    implementation "androidx.paging:paging-runtime:$paging_version"

    // Image library
    def picasso_version = "2.71828"
    implementation "com.squareup.picasso:picasso:$picasso_version"
    implementation 'com.flaviofaria:kenburnsview:1.0.7'


    def coroutines_version = '1.1.1'
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$coroutines_version"
    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-core:$coroutines_version"

    // Leak Canary
    //debugImplementation 'com.squareup.leakcanary:leakcanary-android:1.5.4'
    //releaseImplementation 'com.squareup.leakcanary:leakcanary-android-no-op:1.5.4'

    // Tests
    testImplementation 'junit:junit:4.12'
    testImplementation "androidx.room:room-testing:$room_version"
    testImplementation "androidx.paging:paging-common:$paging_version"

    def robolectric_version = '4.1'
    testImplementation "org.robolectric:robolectric:$robolectric_version"
    androidTestImplementation 'androidx.test:rules:1.1.1'
    androidTestImplementation 'androidx.test:runner:1.1.1'
    androidTestImplementation ('androidx.test.espresso:espresso-core:3.1.0-alpha3', {
        exclude group: 'com.android.support', module: 'support-annotations'
    })

    // FPS
    /*
    debugImplementation "com.github.brianPlummer:tinydancer:0.1.2"
    releaseImplementation "com.github.brianPlummer:tinydancer-noop:0.1.2"
    testImplementation "com.github.brianPlummer:tinydancer-noop:0.1.2"*/
}
